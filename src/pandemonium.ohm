Pandemonium { 
    Program     = Statement+
    Block       = "{" Statement+ "}"
    Statement   = Declaration                         
                | nope ";"                                      -- nope
                | yeet Exp ";"                                  -- yeet
                | print "(" Args ")" ";"                        -- print
                | while "(" Exp ")" Block                       -- while
                | ProcCall                                  
                | ForLoop
                | IfThen
                | Assignment ";"                                -- assignment
    Type        = numType | bool | stringType                   
                | "[" Type "]"                                  -- list
    Variable    = Variable "[" Exp "]"                          -- subscript
                | Variable "." Exp                              -- memberaccess
                | id
    Declaration = Vardec | Funcdec
    Vardec      = now id ":" Type "=" Exp ";"                   -- mutable
                | set id ":" Type "=" Exp ";"	                -- immutable
    Funcdec     = Type id "(" Params ")" Block                  -- function
                | proc id "(" Params ")" Block                  -- procedure
    Assignment  = Increment                                     
                | Variable "=" Exp                              -- base
                | Variable "+=" Exp                             -- add
                | Variable "-=" Exp                             -- sub
    Increment   = ("++" | "--" ) Variable                       -- prefix
                | Variable ("++" | "--")                        -- postfix
    ForLoop     = for "(" Vardec id in Variable ")" Block       -- elementwise
                | for "(" Vardec Exp ";" Exp ")" Block          -- incremental
    IfThen      = if "(" Exp ")" Block ElseIf                   -- complex
                | if "(" Exp ")" Block                          -- simple
    ElseIf      = elif "(" Exp ")" Block ElseIf                 -- complex
                | elif "(" Exp ")" Block                        -- simple
                | ThenElse                                      
    ThenElse    = else Block


    Exp         = Exp "||" Exp2                                 -- binary
                | Exp2
    Exp2        = Exp2 "&&" Exp3                                -- binary
                | Exp3
    Exp3        = Exp4 ("=="|"!=") Exp4                         -- binary
                | Exp4
    Exp4        = Exp5 ("<="|"<"|"C<"|"C="|in|is|">="|">") Exp5 -- binary
                | Exp5
    Exp5        = Exp5 ("+" | "-") Exp6                         -- binary
                | Exp6
    Exp6        = Exp6 ("*"|"//"|"/"|"%") Exp7                  -- binary
                | Exp7
    Exp7        = Exp8 ("^") Exp7                               -- binary
                | Exp8
    Exp8        = ("++" | "--" | "!" | "-") Exp9                -- unary
                | Exp9
    Exp9        = Exp10 ("++" | "--")                           -- unary
                | Exp10
    Exp10       = num
                | true
                | false
                | FuncCall
                | Variable
                | id
                | str
                | "[" Args "]"                                  -- list
                | Lambda
                | "(" Exp ")"                                   -- grouping
    
    FuncCall    = id "(" Args ")"
    ProcCall    = id "(" Args ")" ";"

    Lambda      = "(" Params ")" "->" (Exp | Block)             -- base
                | "(" Params ")" "[" Captures "]" "->" (Exp | Block)    -- capturing
    
    Params      = ListOf<Param, ",">
    Args        = ListOf<Exp, ",">
    Captures    = ListOf<Variable, ",">
    Param       = Type id

    idchar      = "_" | alnum
    now         = "now" ~idchar
    set         = "set" ~idchar
    true        = "true" ~idchar
    false       = "false" ~idchar
    numType     = "num" ~idchar
    bool        = "bool" ~idchar
    stringType  = "string" ~idchar
    proc        = "proc" ~idchar
    for         = "for" ~idchar
    while       = "while" ~idchar
    if          = "if" ~idchar
    elif        = "elif" ~idchar
    else        = "else" ~idchar
    yeet        = "yeet" ~idchar
    nope        = "nope" ~idchar
    print       = "print" ~idchar
    in          = "in" ~idchar
    is          = "is" ~idchar
    str         = strDelimiter (~strDelimiter ~"\n" any)* strDelimiter              -- oneline
                | multiLineDelimiter (~multiLineDelimiter any)* multiLineDelimiter  -- multiline
    character   = ~"\"" ~"\n" any
    multiLineDelimiter = "`"
    strDelimiter = "\""
    id          = ~keyword letter idchar*
    keyword     = now | set | numType | bool | stringType | if | else
                | for | while | yeet | nope | in
    num         = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
    space       += "$" (~"\n" any)* ("\n" | end)                -- comment
}