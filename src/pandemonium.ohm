Pandemonium { 
    Program     = Statement+
    Block       = "{" Statement+ "}"
    Statement   = Declaration                         
                | nope ";"                                      -- nope
                | yeet Exp ";"                                  -- yeet
                | print "(" Args ")" ";"                        -- print
                | while "(" Exp ")" Block                       -- while
                | ProcCall                                  
                | ForLoop
                | IfThen
                | Assignment ";"                                -- assignment
    Type        = numType | bool | stringType | id                   
                | "[" Type "]"                                  -- list
    Variable    = Variable "[" Exp ":" Exp "]"                  -- slice
                | Variable "[" Exp "]"                          -- subscript
                | Variable "." Exp                              -- memberaccess
                | new id "(" Args ")"                           -- instantiation
                | "<-" Variable                                 -- unpacking
                | id
    Declaration = Vardec | Funcdec | StructDec | ClassDec
    Vardec      = now CallId ":" Type "=" Exp ";"               -- mutable
                | set CallId ":" Type "=" Exp ";"	            -- immutable
    Funcdec     = Type id "(" Params ")" Block                  -- function
                | proc id "(" Params ")" Block                  -- procedure
    Assignment  = Increment                                     
                | Variable "=" Exp                              -- base
                | Variable "+=" Exp                             -- add
                | Variable "-=" Exp                             -- sub
    Increment   = ("++" | "--" ) Variable                       -- prefix
                | Variable ("++" | "--")                        -- postfix
    ForLoop     = for "(" Vardec id in Variable ")" Block       -- elementwise
                | for "(" Vardec Exp ";" Exp ")" Block          -- incremental
    IfThen      = if "(" Exp ")" Block ElseIf                   -- complex
                | if "(" Exp ")" Block                          -- simple
    ElseIf      = elif "(" Exp ")" Block ElseIf                 -- complex
                | elif "(" Exp ")" Block                        -- simple
                | ThenElse                                      
    ThenElse    = else Block
    StructDec   = struct id ObjectBlock
    ClassDec    = class id ObjectBlock
    ObjectBlock = "{" (Statement | VisibilityLabel)+ "}"
    VisibilityLabel = (public | private) ":"


    Exp         = Exp "or" Exp2                                 -- binary
                | Exp2
    Exp2        = Exp2 "and" Exp3                                -- binary
                | Exp3
    Exp3        = Exp4 ("=="|"!=") Exp4                         -- binary
                | Exp4
    Exp4        = Exp5 ("<="|"<"|"C<"|"C="|in|is|">="|">") Exp5 -- binary
                | Exp5
    Exp5        = Exp5 ("+" | "-") Exp6                         -- binary
                | Exp6
    Exp6        = Exp6 ("*"|"//"|"/"|"%") Exp7                  -- binary
                | Exp7
    Exp7        = Exp8 ("^") Exp7                               -- binary
                | Exp8
    Exp8        = ("++" | "--" | "!" | "-") Exp9                -- unary
                | Exp9
    Exp9        = Exp10 ("++" | "--")                           -- unary
                | Exp10
    Exp10       = num
                | true
                | false
                | TemplateLiteral
                | str
                | FuncCall
                | Variable
                | "[" Args "]"                                  -- list
                | Lambda
                | "(" Exp ")"                                   -- grouping
    
    FuncCall    = CallId "(" Args ")"
    ProcCall    = CallId "(" Args ")" ";"
    CallId      = CallId1 "." CallId                            -- memberaccess
                | id
    CallId1     = CallId "[" Exp "]"                            -- listaccess
                | id                             

    Lambda      = "(" Params ")" "->" (Exp | Block)             -- base
                | "(" Params ")" "[" Captures "]" "->" (Exp | Block)    -- capturing
    
    Params      = ListOf<Param, ",">
    Args        = ListOf<Exp, ",">
    Captures    = ListOf<Variable, ",">
    Param       = Type id

    idchar      = "_" | alnum
    now         = "now" ~idchar
    set         = "set" ~idchar
    true        = "true" ~idchar
    false       = "false" ~idchar
    numType     = "num" ~idchar
    bool        = "bool" ~idchar
    stringType  = "string" ~idchar
    proc        = "proc" ~idchar
    for         = "for" ~idchar
    while       = "while" ~idchar
    if          = "if" ~idchar
    elif        = "elif" ~idchar
    else        = "else" ~idchar
    yeet        = "yeet" ~idchar
    nope        = "nope" ~idchar
    print       = "print" ~idchar
    in          = "in" ~idchar
    is          = "is" ~idchar
    struct      = "struct" ~idchar
    class       = "class" ~idchar
    public      = "public" ~idchar
    private     = "private" ~idchar
    new         = "new" ~idchar

    str         = strDelimiter (~strDelimiter ~"\n" any)* strDelimiter
    character   = ~"\"" ~"\n" any
    strDelimiter = "\""

    templateLiteralDelimiter = "`"
    TemplateLiteral     = templateLiteralDelimiter TemplateLiteralBody
    TemplateLiteralBody = ~templateLiteralDelimiter "#" "{" Exp "}" TemplateLiteralBody -- expression
                        | ~templateLiteralDelimiter any TemplateLiteralBody             -- character
                        | templateLiteralDelimiter
    
    id          = ~keyword letter idchar*
    keyword     = now | set | numType | bool | stringType | if | else
                | for | while | yeet | nope | in
    num         = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
    space       += "$" (~"\n" any)* ("\n" | end)                -- comment
}